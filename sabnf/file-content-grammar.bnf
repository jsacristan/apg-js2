;*******************************************************************************
;  APG - an ABNF Parser Generator
;  JavaScript APG Version 2.0.0
;  Copyright (C) 2015 Lowell D. Thomas, all rights reserved
;
;  author:  Lowell D. Thomas
;  email:   lowell@coasttocoastresearch.com
;*******************************************************************************
;
; Syntax of a file of Generator arguments.
;
; 1. arguments are tokens of printing characters (9, 32-126)
; 2. arguments are separated by spaces, tabs or line ends
; 3. a line end can be a CRLF pair, a single LF or a single CR
; 2. tokens with spaces or tabs must be quoted ("ab c" or 'ab c')
; 3. quoted strings may have leading unquoted characters (abc"xyz")
; 4. quoted strings end with a matching quote or a line end ("abc defLF)
;
; Quoted strings are identified so that quotes can be removed
; in the same way the main() program does with its arguments.
;
file 		= owsp [any-token *(wsp any-token)] owsp
any-token 	= 1*(dtoken / stoken / token)
token 		= 1*any-safe 
stoken 		= squote svalue (squote / &line-end / "")
dtoken 		= dquote dvalue (dquote / &line-end / "")
svalue      = *any-no-squote
dvalue      = *any-no-dquote
squote 		= %d39
dquote 		= %d34

any-safe 		= %d33 / %d35-38 / %d40-126 ; any printing character except space, single or double quote
any-no-space 	= %d33-126 					; any printing character except space or tab
any-no-squote 	= %d32-38 / %d40-126 / %d9		; any printing character except single quote
any-no-dquote 	= %d32-33 / %d35-126 / %d9 		; any printing character except double quote

; core
line-end 	= %d13.10 / %d10 / %d13
comment     = "#" *(%d32-126/%d9)
sp 			= %d32 / %d9 / [comment] line-end
wsp 		= 1*sp
owsp 		= *sp
